// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16    (in = DFFOut, out = IncOut);
    Mux16    (a = DFFOut, b = IncOut, sel = inc, out = IncOrOut);
    Mux16    (a = IncOrOut, b = in, sel = load, out = IncOrOutOrLoad);
    Mux16    (a = IncOrOutOrLoad, b = false, sel = reset, out = DFFIn);
    Or       (a = load, b = inc, out = LoadOrInc);
    Or       (a = LoadOrInc, b = reset, out = DFFLoad);
    Register (in = DFFIn, load = DFFLoad, out = out, out = DFFOut);
}
